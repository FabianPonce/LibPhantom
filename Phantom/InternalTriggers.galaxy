//---------------------------------------------------------------------------------
// LibPhantom
// A Galaxy-powered SC2 Phantom Map API
//---------------------------------------------------------------------------------
// This code is free to use for your own independent projects provided
// you give credit to the original author, Motive.
//
// Project Page: <https://github.com/Motive/LibPhantom/>
// For modding support, visit <http://www.sc2mapster.com/>.
//---------------------------------------------------------------------------------

bool libPhantom_TriggerAllianceSubmitButtonClicked(bool checkConds, bool runActions)
{
	int plr = EventPlayer();
	int i = 0;
	
	while( i < c_phantomMaxPlayers)
	{
		if( libPhantom_AllianceCheckbox[plr][i] <= 0 || libPhantom_IsPlayerSlave(i) )
		{
			i += 1;
			continue;
		}
		
		// Set proper alliances
		libPhantom_AttemptSetAllianceToPlayer(plr, i, libNtve_gf_DialogItemIsChecked(libPhantom_AllianceCheckbox[plr][i], plr), libNtve_gf_DialogItemIsChecked(libPhantom_VisionCheckbox[plr][i], plr));
		i += 1;
	}
	
	
	// ...and get rid of the dialog
	libPhantom_ToggleAllianceDialog(EventPlayer());
	return true;
}

bool libPhantom_TriggerClickVoteButton(bool checkConds, bool runActions)
{
	int plr = EventPlayer();
	int i;
	int o;
	if( libPhantom_PlayerHasVoted(plr) )
	{
		return true;
	}
	
	libPhantom_PlayerVoted(plr);
	
	// Vote for Game Mode!
	i = 0;
	while(i < c_phantomGameMode_MAX)
	{
		if( libNtve_gf_DialogItemIsChecked(libPhantom_GameModes[i].dialogControl, plr) )
		{
			// We voted for this
			libPhantom_GameModes[i].votes += 1;
			libPhantom_ModeVotedFor[plr] = i;
			break;
		}
		i += 1;
	}
	
	// Votes for Game Variant Groups
	i = 0;
	o = 0;
	while( i < c_phantomGameVariantGroup_MAX)
	{
		o = 0;
		while(o < c_phantomMaxVariantsPerGroup)
		{
			if( libPhantom_GameVariants[i].variants[o].name != "" && libNtve_gf_DialogItemIsChecked(libPhantom_GameVariants[i].variants[o].dialogControl, plr) )
			{
				// We voted for this.
				libPhantom_GameVariants[i].variants[o].votes += 1;
				break; // Out of inner loop, to outer loop.
			}
			o += 1;
		}
		i += 1;
	}
	
	// And now votes for the standalone variants.
	i = 0;
	while(i < c_phantomGameVariant_MAX)
	{
		if( libNtve_gf_DialogItemIsChecked(libPhantom_StandaloneGameVariants[i].dialogControl, plr) )
		{
			libPhantom_StandaloneGameVariants[i].votes += 1;
		}
		i += 1;
	}
	
	// Hide the vote dialog now
	TriggerExecute(TriggerCreate("libPhantom_TriggerClickSkipButton"), true, true);
	TriggerExecute(TriggerCreate("libPhantom_TriggerAskPreferredRole"), true, true);
	return true;
}

bool libPhantom_TriggerAskPreferredRole(bool checkConds, bool runActions)
{
	libPhantom_BuildPreferredRoleDialog(EventPlayer());
	return true;
}

bool libPhantom_TriggerChosePreferredRole(bool checkConds, bool runActions)
{
	int i = 0;
	int p = EventPlayer();
	
	// No choosing twice
	if( libPhantom_RolePrefferedChosen[p] ) {
		return true;
	}
	
	while(i < c_phantomRole_MAX)
	{
		if( libPhantom_RoleVoteButtons[p][i] == EventDialogControl() )
		{
			// We preferred this role
			libPhantom_RolePrefferedChosen[p] = true;
			libPhantom_PreferredRole[p] = i;
			if( libPhantom_RolePreferredPlayers[i] == null ) {
				libPhantom_RolePreferredPlayers[i] = PlayerGroupEmpty();
			}
			PlayerGroupAdd(libPhantom_RolePreferredPlayers[i], p);
			DialogDestroy(DialogControlGetDialog(EventDialogControl()));
			return true;
		}
		i += 1;
	}
	
	// We get here if we pick Random.
	DialogDestroy(DialogControlGetDialog(EventDialogControl()));
	return true;
}

bool libPhantom_TriggerClickSkipButton(bool checkConds, bool runActions)
{
	int i = 0;
	while(i < 3+c_phantomGameVariantGroup_MAX)
	{
		if( libPhantom_VoteDialogs[i] != -1 )
		{
			DialogSetVisible( libPhantom_VoteDialogs[i], PlayerGroupSingle(EventPlayer()), false );
		}
		i += 1;
	}
	return true;
}

bool libPhantom_TriggerGameModeDialogCheckboxClicked(bool checkConds, bool runActions)
{
	int plr = EventPlayer();
	int dialogControl = EventDialogControl();
	
	if( !libNtve_gf_DialogItemIsChecked(dialogControl, plr) ) 
	{
		return true;
	}
	
	libPhantom_ResetGameModeVotes(plr, dialogControl);
	return true;
}

// Keeps stuff in "groups" from being checked together, only.
bool libPhantom_TriggerDialogCheckboxClicked(bool checkConds, bool runActions)
{
	int plr = EventPlayer();
	int dialogControl = EventDialogControl();
	int i = 0;
	int o = 0;
	if( !libNtve_gf_DialogItemIsChecked(dialogControl, plr) ) 
	{
		return true;
	}
	
	// Uncheck any others in this group.
	i = 0;
	while(i < c_phantomGameVariantGroup_MAX)
	{
		o = 0;
		while(o < c_phantomMaxVariantsPerGroup)
		{
			if( libPhantom_GameVariants[i].variants[o].dialogControl == dialogControl )
			{
				// We have a match!
				// Reset all of this guy's group.
				libPhantom_ResetGameVariantGroup(plr, i, dialogControl);
				return true;
			}

			o += 1;
		}
		
		i += 1;
	}
	return true;
}

bool libPhantom_TriggerDeceptionDialogSubmitClicked(bool checkConds, bool runActions)
{

	if( libNtve_gf_DialogItemIsChecked(libPhantom_DeceptionCheckbox[EventPlayer()], EventPlayer()) )
	{
		libPhantom_PlayerHideSupplyCap(EventPlayer(), true);
	} else {
		libPhantom_PlayerHideSupplyCap(EventPlayer(), false);
	}

	// And get rid of the deception dialog, last.
	libPhantom_ToggleDeceptionDialog(EventPlayer());
	
	return true;
}

bool libPhantom_TriggerDeceptionScreenButtonClicked(bool checkConds, bool runActions)
{
	libPhantom_ToggleDeceptionDialog(EventPlayer());
	return true;
}

bool libPhantom_TriggerAllianceScreenButtonClicked(bool checkConds, bool runActions)
{
	libPhantom_ToggleAllianceDialog(EventPlayer());
	return true;
}

bool libPhantom_TriggerPeriodicResourceHandout(bool checkConds, bool runActions)
{
	int i;
	int r;
	while(i < c_phantomMaxPlayers)
	{
		r = libPhantom_GetPlayerRole(i, false);
		if( !libPhantom_IsPlayerAlive(i) ) {
			i += 1;
			continue;
		}
		
		// Do we have periodic mineral collection powers?
		if( libPhantom_Roles[r].incomeMinerals > 0 ) {
			PlayerModifyPropertyInt(i, c_playerPropMinerals, c_playerPropOperAdd, libPhantom_Roles[r].incomeMinerals);
		}
		
		// Do we have periodic gas collection powers?
		if( libPhantom_Roles[r].incomeGas > 0 ) {
			PlayerModifyPropertyInt(i, c_playerPropVespene, c_playerPropOperAdd, libPhantom_Roles[r].incomeGas);
		}
		i += 1;
	}
	return true;
}

bool libPhantom_TriggerMapInitialization(bool checkConds, bool runActions) 
{
	int i;
	int dialog;
	int seconds = 30;
	
	libPhantom_LoadBanks();
	libPhantom_EnsureUniquePlayerColors();
	FogSetEnabled(false);
	GameSetSpeedValue(c_gameSpeedFaster);

	MeleeInitUnits();
	MeleeInitResources();
	i = 0;
	while(i != c_phantomMaxPlayers)
	{
		MeleeSetOption(i, c_meleeOptionReveal, true);
		PlayerScoreValueEnableAll(i, false);
		libPhantom_OriginalColor[i] = PlayerGetColorIndex(i, false);
		i += 1;
	}
	// Disable trading/alliance menu
	UISetFrameVisible(PlayerGroupActive(), c_syncFrameTypeTeamResourceButton, false);
	UISetFrameVisible(PlayerGroupActive(), c_syncFrameTypeAllianceButton, false);
	
	// Temp: Disable hotkeys and stuff
	UISetCommandAllowed(PlayerGroupActive(), c_uiCommandAllowButtons, false);
	UISetCommandAllowed(PlayerGroupActive(), c_uiCommandAllowHotkeys, false);
	UISetCommandAllowed(PlayerGroupActive(), c_uiCommandAllowSmartClick, false);
	
	libNtve_gf_SetPlayerGroupAlliance( PlayerGroupActive(), 1 ); // Ally w/ Shared Vision to start.
	
	libNtve_gf_HideGameUI(false, PlayerGroupActive());
	CinematicFade( false, 0.0, c_fadeStyleNormal, Color(0, 0, 0), 0, false);

	libPhantom_ShowVotingDialog();
	// Countdown, anyone?
	while( libPhantom_VotingInProgress() && seconds > 0)
	{
		dialog = DialogCreate(200, 200, c_anchorBottom, 0, 50, false);
		DialogSetImageVisible(dialog, false);
		DialogSetVisible(dialog, PlayerGroupActive(), true);
		libNtve_gf_CreateDialogItemLabel(dialog, 200, 200, c_anchorCenter, 0, 0, IntToText(seconds), Color(100,0,0),true,0.1);
		libNtve_gf_SetDialogItemStyle(DialogControlLastCreated(), "ChallengeReady", PlayerGroupActive());
		// Now we get written out over 0.1 and wait for 0.4 seconds
		Wait(0.4, c_timeReal);
		DialogControlFadeTransparency(DialogControlLastCreated(), PlayerGroupActive(), 0.5, 100);
		
		// Fading out over the other 0.5
		Wait(0.5, c_timeReal);
		
		seconds -= 1;
		DialogDestroy(dialog);
	}
	
	// Destroy dialogs if someone didn't vote.
	DialogDestroyAll();
	
	CinematicFade( true, 0, c_fadeStyleNormal, Color(0, 0, 0), 0, false);
	libNtve_gf_HideGameUI(true, PlayerGroupActive());
	
	// Temp: Re-Enable hotkeys and stuff
	UISetCommandAllowed(PlayerGroupActive(), c_uiCommandAllowButtons, true);
	UISetCommandAllowed(PlayerGroupActive(), c_uiCommandAllowHotkeys, true);
	UISetCommandAllowed(PlayerGroupActive(), c_uiCommandAllowSmartClick, true);

	// Activate the game modes based on everyone's votes.
	libPhantom_TallyVotes();

	i = 0;
	while(i < c_phantomMaxPlayers)
	{
		if( !PlayerGroupHasPlayer(PlayerGroupActive(), i) ) {
			i += 1;
			continue;
		}
		libPhantom_InitialMinerals[i] = PlayerGetPropertyInt(i, c_playerPropMinerals);
		libPhantom_InitialGas[i] = PlayerGetPropertyInt(i, c_playerPropVespene);
		
		// Do auto-harvest.
		libLbty_gf_OrderWorkerstoGatherNearbyResources(RegionEntireMap(), i);
		
		libPhantom_PlayerIncreasePlayedGamesScore(i);
		i += 1;
	}

	libPhantom_BuildHelpMenuTips();

	// Draw Screen Buttons for Alliance/Deception
	libNtve_gf_DisplayScreenButton(1, StringToText("<s val=\"MercCategory\">Alliances</s>"), 200, 80, c_anchorTopLeft, 30, 50, TriggerCreate("libPhantom_TriggerAllianceScreenButtonClicked") );
	libNtve_gf_SetScreenButtonBorderImage(1, "Assets\\Textures\\ui_mission_hackingbutton_normalpressed.dds", "Assets\\Textures\\ui_mission_hackingbutton_normaloverpressedover.dds", c_triggerImageTypeBorder);
	libNtve_gf_DisplayScreenButton(2, StringToText("<s val=\"MercCategory\">Deception</s>"), 200, 80, c_anchorTopLeft, 30, 130, TriggerCreate("libPhantom_TriggerDeceptionScreenButtonClicked") );
	libNtve_gf_SetScreenButtonBorderImage(2, "Assets\\Textures\\ui_mission_hackingbutton_normalpressed.dds", "Assets\\Textures\\ui_mission_hackingbutton_normaloverpressedover.dds", c_triggerImageTypeBorder);
	return true;
}

bool libPhantom_TriggerGameModeSelected(bool checkConds, bool runActions)
{
	int i;
	string message;
	text toSend;
	
	// Do we have enough players?
	if( !libPhantom_HasSufficientPlayers() ) {
		libPhantom_SendInformationMessageToAll("Phantom requires additional players.");
		i = 0;
		while( i < c_phantomMaxPlayers ) {
			if( PlayerGroupHasPlayer(PlayerGroupActive(), i) ) {
				GameOver(i, c_gameOverTie, true, true);
			}
			i += 1;
		}
		return true;
	}
	
	// Assign the players to appropriate roles.
	libPhantom_AssignPlayersToRoles();
	
	i = 0;
	while( i < c_phantomMaxPlayers )
	{
		if( !libPhantom_IsPlayerAlive(i) ) {
			i += 1;
			continue;
		}
		message = "";
		// Build message with current game mode and their role and send it to 'em.
		message = libPhantom_GetCurrentGameModeName() + " has been chosen.<n />You are a ";
		message += libPhantom_BuildRoleName(libPhantom_GetPlayerRole(i, true)) + ". ";
		message += libPhantom_Roles[libPhantom_GetPlayerRole(i, true)].objective;
		
		libPhantom_SendInformationMessageToGroup( PlayerGroupSingle(i), message );
		
		i += 1;
	}
	
	libPhantom_SetGameStarted(true);
	return true;
}

bool libPhantom_TriggerUnitDied(bool checkConds, bool runActions)
{
	int plr = UnitGetOwner(EventUnit());
	int killingPlr = EventUnitDamageSourcePlayer();
	int cost;
	fixed bountyPct;
	
	if( !(killingPlr == -1 || EventUnitDamageSourceUnit() == null || killingPlr == plr) )
	{
		bountyPct = libPhantom_Roles[libPhantom_GetPlayerRole(killingPlr, false)].killBounty;
		// Get mineral cost of now-dead unit.
		cost = FixedToInt(UnitTypeGetCost( UnitGetType(EventUnit()), c_unitCostMinerals) * bountyPct);
		PlayerModifyPropertyInt( killingPlr, c_playerPropMinerals, c_playerPropOperAdd, cost);
		// and now gas cost.
		cost = FixedToInt(UnitTypeGetCost( UnitGetType(EventUnit()), c_unitCostVespene) * bountyPct);
		PlayerModifyPropertyInt( killingPlr, c_playerPropVespene, c_playerPropOperAdd, cost);
	}
	
	// Is this player now dead as a result of this death?
	if( libPhantom_IsPlayerReadyToDie(plr) ) {
		libPhantom_EliminatePlayer(plr, killingPlr);
	}

	return true;
}

bool libPhantom_TriggerPlayerLeft(bool checkConds, bool runActions)
{
	if( !libPhantom_IsGameStarted() || !libPhantom_IsGameOver() ) {
		libPhantom_EliminatePlayer(EventPlayer(), 0);
	}
	
	// Okay, so, here's the deal: Blizzard decided to be stupid and give us no option to disable resource splitting. So we have to handle that shit.
	// Bleh. So let's reset everyone's minerals to what they used to be.
	PlayerGroupLoopBegin(PlayerGroupActive());
	while( !PlayerGroupLoopDone() )
	{
		PlayerModifyPropertyInt(PlayerGroupLoopCurrent(), c_playerPropMinerals, c_playerPropOperSetTo, libPhantom_CurrentMinerals[PlayerGroupLoopCurrent()]);
		PlayerModifyPropertyInt(PlayerGroupLoopCurrent(), c_playerPropVespene, c_playerPropOperSetTo, libPhantom_CurrentMinerals[PlayerGroupLoopCurrent()]);
		PlayerGroupLoopStep();
	}
	return true;
}

bool libPhantom_TriggerPeriodicMineralTracker(bool checkConds, bool runActions)
{
	int i = 0;
	while(i < c_phantomMaxPlayers)
	{
		if( PlayerGroupHasPlayer(PlayerGroupActive(), i) )
		{
			libPhantom_CurrentMinerals[i] = PlayerGetPropertyInt(i, c_playerPropMinerals);
			libPhantom_CurrentGas[i] = PlayerGetPropertyInt(i, c_playerPropVespene);
		}
		i += 1;
	}
	return true;
}

bool libPhantom_TriggerPeriodicGameLoop(bool checkConds, bool runActions)
{
	int i;
	int x;
	playergroup p;
	text message;
	text messageSend;
	bool b;
	
	// Do not increment unless game is in progress
	if( !libPhantom_IsGameStarted() )
	{
		return true;
	}
	
	// Increment game timer counter
	libPhantom_GameTime += 1;
	
	i = 0;
	while(i < c_phantomRole_MAX )
	{
		b = (libPhantom_GameModes[libPhantom_GetCurrentGameMode()].participatingRoles & libPhantom_Roles[i].myMask) != 0;
		// Is it time to strengthen this role?
		if( b && libPhantom_Roles[i].periodicStrengthenTimer > 0 && libPhantom_GetGameTime() % libPhantom_Roles[i].periodicStrengthenTimer == 0 ) {
			libPhantom_StrengthenRole(i);
			if( libPhantom_Roles[i].onStrengthen != null ) {
				TriggerExecute( libPhantom_Roles[i].onStrengthen, true, true );
			}
		}
		
		// It is time to reveal.
		if( libPhantom_Roles[i].revealAt != -1 && libPhantom_Roles[i].revealAt == libPhantom_GetGameTime() ) {
			libPhantom_RevealForRole(i);
		}
		i += 1;
	}
	
	if( libPhantom_GetGameTime() % c_phantomWorkerPingTime == 0 )
	{
		x = 0;
		while(x < c_phantomMaxPlayers)
		{
			if( libPhantom_IsPlayerAlive(x) && libPhantom_PlayerHasOnlyWorkers(x) )
			{
				libPhantom_PingWorkers(x);
			}
			x += 1;
		}
	}
	
	// Game Mode onPeriodic calls
	if( libPhantom_GameModes[libPhantom_GetCurrentGameMode()].onPeriodic != null )
	{
		TriggerExecute( libPhantom_GameModes[libPhantom_GetCurrentGameMode()].onPeriodic, true, true);
	}
	return true;
}

bool libPhantom_TriggerUnitIssuedOrder(bool checkConds, bool runActions)
{
	unit u = OrderGetTargetUnit(EventUnitOrder());
	int plr = EventPlayer();
	int owner;
	// Are we attacking our master?
	if( u == null ) {
		return true;
	}

	owner = UnitGetOwner(u);
	// But what if the owner of the unit is a slave, too? We must use the owner of this player if so.
	if( libPhantom_IsPlayerSlave(owner) ) {
		owner = libPhantom_GetPlayerSlaveOwner(owner);
	}
	
	if( libPhantom_IsPlayerSlave(plr) && libPhantom_GetPlayerSlaveOwner(plr) == owner ) {
		// We're attacking our owner!
		UnitIssueOrder(EventUnit(), Order(AbilityCommand("stop", 0)), c_orderQueueReplace);
	}
	
	return true;
}

// Galaxy bug: Must end on newline or errors will be reported on the wrong line
